plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "dagger.hilt.android.plugin"
    id "com.google.devtools.ksp"
    id "com.apollographql.apollo" version "$apollo_version"
}

android {
    namespace "dev.thunderbolt.pokemonpager"
    compileSdk 34

    defaultConfig {
        applicationId "dev.thunderbolt.pokemonpager"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
        freeCompilerArgs += [
                "-opt-in=androidx.compose.material3.ExperimentalMaterial3Api",
                "-opt-in=androidx.lifecycle.viewmodel.compose.SavedStateHandleSaveableApi",
                "-opt-in=androidx.paging.ExperimentalPagingApi",
                "-opt-in=androidx.compose.foundation.layout.ExperimentalLayoutApi",
        ]
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion "1.5.1"
    }

    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
}

apollo {
    service("pokemon") {
        packageName.set("dev.thunderbolt.pokemonpager.data")
        introspection {
            // RUN "./gradlew :app:downloadPokemonApolloSchemaFromIntrospection" TO DOWNLOAD THE SCHEMA
            endpointUrl.set("https://graphql-pokeapi.graphcdn.app/graphql")
            schemaFile.set(file("src/main/graphql/schema.graphqls"))
        }
    }
}

dependencies {
    // KOTLIN
    implementation platform("org.jetbrains.kotlin:kotlin-bom:$kotlin_version")
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.3"

    // ANDROID
    implementation "androidx.core:core-ktx:1.13.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycle_version"
    implementation "androidx.navigation:navigation-compose:2.8.5"

    // COMPOSE
    implementation platform("androidx.compose:compose-bom:$compose_version")
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-graphics"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.compose.material3:material3"
    debugImplementation "androidx.compose.ui:ui-tooling"
    debugImplementation "androidx.compose.ui:ui-test-manifest"

    // PAGING
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    implementation "androidx.paging:paging-compose:$paging_version"

    // HILT
    implementation "com.google.dagger:hilt-android:$hilt_version"
    ksp "com.google.dagger:hilt-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-navigation-compose:1.2.0"

    // APOLLO
    implementation "com.apollographql.apollo:apollo-runtime:$apollo_version"

    // ROOM
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-paging:$room_version"

    // COIL
    implementation "io.coil-kt:coil-compose:2.4.0"

    // TEST
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.2.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.6.1"
    androidTestImplementation platform("androidx.compose:compose-bom:$compose_version")
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
}
